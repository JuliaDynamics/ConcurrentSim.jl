{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Event Latency\n",
    "\n",
    "## Description \n",
    "In this example we show how to separate the time delay between processes from the processes themselves. We model a communications channel, called a `Cable`, where a sender sends messages regularly each `SEND_PERIOD` time units and a receiver listens each `RECEIVE_PERIOD`. The messages in the cable have a delay fo `DELAY_DURATION` until they reach the recevier."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Load Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Simulvent\n",
    "using Semicoroutines\n",
    "import  Simulvent.put\n",
    "import Base.get"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Define Constants"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 23,
   "metadata": {},
   "outputs": [],
   "source": [
    "srand(8710) # set random number seed for reproducibility\n",
    "const SIM_DURATION = 100.\n",
    "const SEND_PERIOD = 5.0\n",
    "const RECEIVE_PERIOD = 3.0;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Define Cable model\n",
    "The `Cable` contains reference to the simulation it is part of, the delay that messages experience, and a store that contains the sent messages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 24,
   "metadata": {},
   "outputs": [],
   "source": [
    "mutable struct Cable\n",
    "    env::Simulation\n",
    "    delay::Float64\n",
    "    store::Store{String}\n",
    "    \n",
    "    function Cable(env::Simulation, delay::Float64)\n",
    "        return new(env, delay, Store{String}(env))\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The latency function is a generator which yields two events: first a `timeout` that represents the transmission delay, then a put event when the message gets stored in the store."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 25,
   "metadata": {},
   "outputs": [],
   "source": [
    "@resumable function latency(env::Simulation, cable::Cable, value::String)\n",
    "    @yield timeout(cable.env, cable.delay)\n",
    "    @yield put(cable.store, value)\n",
    "    end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The `put` and `get` functions allow interaction with the cable (note that these are not `@resumable` because they need to return the result of the operation and not the operation itself)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 26,
   "metadata": {},
   "outputs": [],
   "source": [
    "function put(cable::Cable, value::String)\n",
    "    @process latency(cable.env, cable, value) # results in the scheduling of all events generated by latency\n",
    "end\n",
    "\n",
    "function get(cable::Cable)\n",
    "    get(cable.store) # returns an element stored in the cable store\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The `sender` and `receiver` generators yield events to the simulator."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 27,
   "metadata": {},
   "outputs": [],
   "source": [
    "@resumable function sender(env::Simulation, cable::Cable)\n",
    "    while true\n",
    "        @yield timeout(env, SEND_PERIOD)\n",
    "        value = \"sender sent this at $(now(env))\"\n",
    "        put(cable, value)\n",
    "    end\n",
    "end\n",
    "\n",
    "@resumable function receiver(env::Simulation, cable::Cable)\n",
    "    while true\n",
    "        @yield timeout(env, RECEIVE_PERIOD)\n",
    "        msg = @yield get(cable)\n",
    "        println(\"Received this at $(now(env)) while $msg\")\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Create simulation, register events, and run!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 29,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Received this at 15.0 while sender sent this at 5.0\n",
      "Received this at 20.0 while sender sent this at 10.0\n",
      "Received this at 25.0 while sender sent this at 15.0\n",
      "Received this at 30.0 while sender sent this at 20.0\n",
      "Received this at 35.0 while sender sent this at 25.0\n",
      "Received this at 40.0 while sender sent this at 30.0\n",
      "Received this at 45.0 while sender sent this at 35.0\n",
      "Received this at 50.0 while sender sent this at 40.0\n",
      "Received this at 55.0 while sender sent this at 45.0\n",
      "Received this at 60.0 while sender sent this at 50.0\n",
      "Received this at 65.0 while sender sent this at 55.0\n",
      "Received this at 70.0 while sender sent this at 60.0\n",
      "Received this at 75.0 while sender sent this at 65.0\n",
      "Received this at 80.0 while sender sent this at 70.0\n",
      "Received this at 85.0 while sender sent this at 75.0\n",
      "Received this at 90.0 while sender sent this at 80.0\n",
      "Received this at 95.0 while sender sent this at 85.0\n"
     ]
    }
   ],
   "source": [
    "env = Simulation()\n",
    "cable = Cable(env, 10.)\n",
    "@process sender(env, cable)\n",
    "@process receiver(env, cable)\n",
    "\n",
    "run(env, SIM_DURATION)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
